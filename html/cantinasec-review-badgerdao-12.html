<!doctype html>
<html>
  <head>
    <meta charset="utf-8">
    <link rel="stylesheet" type="text/css" href="css/style.css">
    <script type="text/javascript" src="lib/jquery.js"></script>
    <script type="text/javascript" src="lib/jquery.emoji.js"></script>
    <title>Last CDP cannot be closed and user cannot recover `~2.2 stETH` from the CDP</title>
  </head>
  <body>
    <div class="container">
      <header>
        <p>
          <span class="badge open">open</span>
          <span class="url"><a href="https://github.com/cantinasec/review-badgerdao/issues/12">cantinasec/review-badgerdao#12</a></span>
        </p>
        <h1>Last CDP cannot be closed and user cannot recover `~2.2 stETH` from the CDP</h1>
      </header>
      <div class="comments">
        <div class="comment">
          <div class="meta">
            <a class="person" href="https://github.com/StErMi">StErMi</a>
            <time>8/10/2023</time>
          </div>
          <div class="body">
            <p><strong>Context:</strong> <a href="https://github.com/Badger-Finance/ebtc/blob/b2f641aa20615978544547e41a4c2be642252ade/packages/contracts/contracts/CdpManagerStorage.sol#L500-L505">CdpManagerStorage.sol#L500-L505</a></p>
<p><strong>Description:</strong> The system is designed to <strong>not allow</strong> the closure and removal of the last active CDP. This required and applied each time an operation (close, redeem, liquidate) tries to close a CDP.</p>
<p>Because of this check, that the last CDP can only be &quot;adjusted&quot; by the user. The user will be able to: </p>
<ul>
<li>Repay at max <code>cdp.debt - 1 wei</code> of debt (repaying all the debt will revert the adjust operation).</li>
<li>Withdraw at max <code>cdp.coll - 2 ether</code> of collateral (withdrawing more than that would revert because of the TCR check or because of the <code>MIN_NET_COLL</code> check).</li>
</ul>
<p>The result is that the user will not be able to withdraw the <code>MIN_NET_COLL</code> amount of collateral and the <code>LIQUIDATOR_REWARD</code> amount of collateral provided at the creation of the CDP for a total of <code>~2.2 stETH</code> (the final amount of <code>stETH</code> depends on the conversion between <code>stETH shares</code> and <code>stETH</code> at the moment of the operation):</p>
<pre><code class="language-solidity">// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;
import &quot;forge-std/Test.sol&quot;;
import {eBTCBaseInvariants} from &quot;./BaseInvariants.sol&quot;;

contract FastCloseTest is eBTCBaseInvariants {
    uint256 public constant MCR = 1100000000000000000; // 110%
    uint256 public constant DECIMAL_PRECISION = 1 ether;
    uint256 public constant MIN_COLLAERAL_SIZE = 2 ether;
    uint256 public constant GAS_STIPEND = 0.2 ether;

    address user1;
    address user2;
    bytes32 cdp1;
    bytes32 cdp2;

    function setUp() public override {
        super.setUp();

        // current value of the shares on lido (more or less)
        // collateral.setEthPerShare(1 ether);
        // priceFeedMock.setPrice((1 ether * DECIMAL_PRECISION) / 15 ether);

        connectCoreContracts();
        connectLQTYContractsToCore();
    }

    function testCloseLastCdpRevert() public {
        // Assume we are at a point where some users have opened the CDP, redempted, liquidated, closed and so on
        // We are at a point where there is only a user with an open CDP
        // The system is designed to revert always when a CDP is being closed but it&#39;s the last one active
        // This mean that the last CDP cannot be closed and the user won&#39;t be able to remove the MIN_NET_COLL
        // and LIQUIDATOR_REWARD for a total of ~2.2stETH (it depends on the share value at closing time)

        // setup the test
        address payable[] memory users;
        users = _utils.createUsers(2);
        user1 = users[0];
        vm.label(user1, &quot;user_1&quot;);
        _dealCollateralAndPrepForUse(user1);

        // user1 creates a CDP
        uint256 borrowedAmount = _utils.calculateBorrowAmount(
            100 ether,
            priceFeedMock.fetchPrice(),
            CCR
        );
        vm.prank(user1);
        cdp1 = borrowerOperations.openCdp(borrowedAmount, &quot;hint&quot;, &quot;hint&quot;, 100 ether + GAS_STIPEND);

        // user1 tries to close the CDP but it reverts
        vm.prank(user1);
        vm.expectRevert(&quot;CdpManager: Only one cdp in the system&quot;);
        borrowerOperations.closeCdp(cdp1);
    }

    function testAdjustLastCdpFundsLeftInProtocol() public {
        // Assume we are at a point where some users have opened the CDP, redempted, liquidated, closed and so on
        // We are at a point where there is only a user with an open CDP
        // The system is designed to revert always when a CDP is being closed but it&#39;s the last one active
        // This mean that the last CDP cannot be closed and the user won&#39;t be able to remove the MIN_NET_COLL
        // and LIQUIDATOR_REWARD for a total of ~2.2stETH (it depends on the share value at closing time)

        // setup the test
        address payable[] memory users;
        users = _utils.createUsers(2);
        user1 = users[0];
        vm.label(user1, &quot;user_1&quot;);
        _dealCollateralAndPrepForUse(user1);

        // user1 creates a CDP
        uint256 borrowedAmount = _utils.calculateBorrowAmount(
            100 ether,
            priceFeedMock.fetchPrice(),
            CCR
        );
        vm.prank(user1);
        cdp1 = borrowerOperations.openCdp(borrowedAmount, &quot;hint&quot;, &quot;hint&quot;, 100 ether + GAS_STIPEND);

        // calculate the final amount of collateral and debt of the CDP
        (uint256 debt, uint256 coll, ) = cdpManager.getEntireDebtAndColl(cdp1);

        // the user cannot repay ALL the debt because otherwise it would revert
        vm.prank(user1);
        borrowerOperations.repayEBTC(cdp1, debt - 1, &quot;&quot;, &quot;&quot;);

        // the user cannot withdraw all the collateral. at least MIN_NET_COLL must remain in the CDP
        vm.prank(user1);
        vm.expectRevert(&quot;BorrowerOperations: Cdp&#39;s net coll must be greater than minimum&quot;);
        borrowerOperations.withdrawColl(cdp1, coll - 2 ether + 1, &quot;&quot;, &quot;&quot;);

        vm.prank(user1);
        borrowerOperations.withdrawColl(cdp1, coll - 2 ether, &quot;&quot;, &quot;&quot;);

        // user only have 1 wei of eBTC in the balance
        assertEq(eBTCToken.balanceOf(user1), 1);

        // active pool still have the remaining stETH of the user (MIN_NET_COLL + LIQUIDATOR_REWARD)
        assertEq(collateral.balanceOf(address(activePool)), 2.2 ether);
    }

    function testDAOCDP() public {
        // one possible solution would be to have a DAO CDP that allows the last user to close the CDP
        // the problem with this DAO CDP is that it&#39;s just a &quot;normal&quot; CDP so at any point in time
        // it could be liquidated or redeemed (and closed) and we would be again at the start of the problem

        // setup the test
        address payable[] memory users;
        users = _utils.createUsers(2);
        user1 = users[0];
        address DAO = users[1];
        vm.label(user1, &quot;user_1&quot;);
        vm.label(DAO, &quot;DAO&quot;);
        _dealCollateralAndPrepForUse(user1);
        _dealCollateralAndPrepForUse(DAO);

        // DAO creates a CDP
        uint256 borrowedAmount = _utils.calculateBorrowAmount(
            100 ether,
            priceFeedMock.fetchPrice(),
            CCR
        );

        console.log(&quot;DAO open CDP&quot;);
        vm.prank(DAO);
        bytes32 cdpDAO = borrowerOperations.openCdp(
            borrowedAmount,
            &quot;hint&quot;,
            &quot;hint&quot;,
            100 ether + GAS_STIPEND
        );

        // user1 creates a CDP
        borrowedAmount = _utils.calculateBorrowAmount(100 ether, priceFeedMock.fetchPrice(), CCR);
        console.log(&quot;user1 open CDP&quot;);
        vm.prank(user1);
        cdp1 = borrowerOperations.openCdp(borrowedAmount, &quot;hint&quot;, &quot;hint&quot;, 100 ether + GAS_STIPEND);

        // user now can close it, the DAO CDP cannot be closed
        vm.prank(user1);
        borrowerOperations.closeCdp(cdp1);
    }
}
</code></pre>
<p><strong>Recommendation:</strong> BadgerDAO could consider opening a healthy CDP to allow the last CDP (of the user) to be correctly closed. BadgerDAO needs to consider that this &quot;DAO CDP&quot; is anyway considered as a &quot;normal CDP&quot; and can be at any time liquidated or redeemed if the platform allows it. If this happens, we would return to the start of the scenario, where the last user won&#39;t be able to correctly close the CDP.</p>
<p><strong>BadgerDAO:</strong> We agree with the finding and will ensure the last CDP is provided by the DAO.</p>
<p><strong>Cantina:</strong> Just to be clear; you will &quot;plug in&quot; the last CDP only when it is needed, right? Because otherwise you have to maintain it during the whole life cycle of the protocol. By &quot;maintaining it&quot;, I mean that you need to ensure that it&#39;s always healthy, and it does not get redeemed from (and closed).</p>
<p><strong>BadgerDAO:</strong> Acknowledged.</p>
<p><strong>Cantina:</strong> Acknowledged.</p>

          </div>
        </div>

        <div class="comment">
          <div class="meta">
            <a class="person" href="https://github.com/GalloDaSballo">
              GalloDaSballo
            </a>
            <time>8/16/2023</time>
          </div>
          <div class="body"><p>We agree with the finding and will ensure the last CDP is provided by the DAO</p>
</div>
        </div>
        <div class="comment">
          <div class="meta">
            <a class="person" href="https://github.com/StErMi">
              StErMi
            </a>
            <time>10/3/2023</time>
          </div>
          <div class="body"><p>@GalloDaSballo just to be clear: you will &quot;plugin&quot; the last CDP only when it is needed, right? Because otherwise you have to maintain it during the whole lifecycle of the protocol. For &quot;maintaining it&quot; I mean that you need to ensure that it&#39;s always healthy, and it does not get redeemed from (and closed)</p>
</div>
        </div>
      </div>
    </div>

    <script type="text/javascript">
      $(document).ready(function(){
        $('.comment').each(function(i, d){
          $(d).emoji()
        })
      })
    </script>

  </body>
</html>
